// MRI ArgoJ Tool Set Verion 2.1
// Marc LARTAUD MRI PHIV
// Boite a outils pour faciliter les analyses metrologiques avec la lame Argolight test7

/*

A installer :
- loci_tools.jar Pour lire tous les formats d images
http://downloads.openmicroscopy.org/bio-formats/5.3.2/artifacts/loci_tools.jar
- pour le calcul du plus proche voisin:
https://icme.hpc.msstate.edu/mediawiki/images/9/9a/Nnd_.class

- Objects counter 3D
http://imagejdocu.tudor.lu/lib/exe/fetch.php?media=plugin:analysis:3d_object_counter:3d-oc_.jar
- PDF Writer- 
https://imagej.nih.gov/ij/plugins/download/PDF_Writer.class
http://www.coderanch.com/how-to/content/iText-2.1.7.jar

//Outil parametres
	pour remttre tout a zero
	pour rentrer les renseignements (plateau verificateur microscope ....)
	en fonction dun fichier parametres_microscopes.csv personnalible en respectant les noms 
	("end" en fin d infos pour un microscope)
	a mettre dans \ImageJ\macros\toolsets\ArgoloJ


// Outil calibration
	pour verifier la calibration spatiale du microscope
	ou calibrer si c est pas deja fait

// Outil homogeneite 
	"Gaussian Blur...", "sigma=1"
	en option elimination  de la croix
	"Maximum...", "radius=diagonale entre points
	"Gaussian Blur...", "sigma=moitie entre points
	image 0-100 % tout les 10%
	
// Outil Coalignement 
	detection des points
	pour chaque canal 
		3D objects counter
		mesure du centre de masse xm ym zm
	calcul des distances
	tableau resultats avec moyennes

// Outil dispersion
	3D objects counter
	calcul de distance entre z

// Outil Resolution
	detection des bandes 
	rotation de l image
	selection d une partie de l image
	plot profile
	detection des maxima
	calcul du nombre de bandes resolues
	
// Outil Linearite
	segmentation des plages (16 carres ou 2*16 bandes)
	mesures d intensites
	courbes de regression (avec la courbe d etalonnage)
	
// Geometrie
	calcul le decalage du repositionnement de la platine

// Rapport PDF
	creation et sauvegarde d un rapport

*/

/* parametres_microscopes.csv est un fichier de configuration specifique a chaque plateau avec 
le nom du plateau
les verificateurs
les microscopes avec leur objectifs et les differents canaux
la fin des infos sur un microscope est signalee par : end */


//variables glogales

	var dir_argoloj=getDirectory("macros")+"toolsets\\ArgoloJ\\";
	var param=dir_argoloj+"parametres_microscopes.csv";
	var plateau=RecupPlateau();
	var NomMicroscope="";
	var TypeMicroscope="";
	var NomObjectif="";
	var NaObjectif="";
	var NomVerificateur="";
	var Infos=false;
	var wave=newArray(0); //tableau des longueurs d ondes
	var wave_value=newArray(0);
	var zero="Oui";
	var date="";
	var datef="";
	//Valeurs d etalonnage donnees par argolight avec la lame
	var Argo_intensites_16 = newArray(1,1.49,2.18,2.99,3.71,4.46,5.18,5.79,6.27,6.88,7.4,7.85,8.18,8.65,9.1,9.55);
	var Argo_intensites_32 = newArray(1,1.52,2.23,3.07,3.91,4.68,5.41,6.05,6.64,7.25,7.78,8.25,8.71,9.18,9.63,10.02);

	var title1 = "Resultats Argolight";
	var title2 = "["+title1+"]";
	var fe = title2;

	// parametres par defaut
	var seuil_calibration=newArray("Li",100,255);
	var seuil_homogeneite=newArray("Otsu",100,255);
	var seuil_coalignement_tot=newArray("Default",100,255);
	var seuil_coalignement=newArray("Yen",100,255,1);
	var seuil_dispersion=newArray("Minimum",100,255);
	var seuil_resolution=newArray("Default",100,255);
	var seuil_geometrie=newArray("Default",100,255);
	var radius_calibration=1;
	var radius_homogeneite=1;
	var radius_coalignement=2;
	var radius_dispersion=3;
	var radius_resolution=20;
	var radius_linearite=2;
	var taille_calibration=newArray(50,500);
	var taille_homogeneite=newArray(20,100);
	var taille_coalignement=newArray(0.5,10,1);
	var taille_resolution=newArray(2,10000);
	var taille_linearite=newArray(200,1000000);

	var croix=false;
	
	//Resultats
	//var distance_calibration=0;
	//var std_calibration=0;
	var std_homo=0;
	var Tab_entete=newArray(0);	
	var Tab_result_moy=newArray(0);
	var Tab_ref_resolution=newArray(0);
	var distance_z_dispersion=0;
	var bande_resolution=0;
	var decalage_geometrie=0;
	var unit=0;
	

macro "AutoRun"{
	setTool("rectangle");
	setForegroundColor(255, 255, 255);//premier plan blanc
	setBackgroundColor(0,0,0);// fond noir
	run("Set Measurements...", "area mean centroid center feret's limit redirect=None decimal=9");
	run("Set 3D Measurements", "volume mean_gray_value centroid centre_of_mass dots_size=5 font_size=10 show_numbers white_numbers store_results_within_a_table_named_after_the_image_(macro_friendly) redirect_to=none");
	requires("1.49p");
	MonthNames = newArray("Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec");
	MonthNamesf = newArray("01","02","03","04","05","06","07","08","09","10","11","12");
	getDateAndTime(year, month, dayOfWeek, dayOfMonth, hour, minute, second, msec);
	date="";
	if (dayOfMonth<10) date = date+"0";
	date = date+dayOfMonth+"-"+MonthNames[month]+"-"+year;
	if (dayOfMonth<10) dayOfMonth="0"+dayOfMonth;
	datef = "-"+year+"-"+MonthNamesf[month]+"-"+dayOfMonth;
	efface_images_temp();
	
	
	
}

///////////////////////////////////////////////////////////param//////////////////////////////////////////////////////

macro "Parametres Action Tool - C333D18D28D34D37D38D39D3cD44D45D46D47D48D49D4aD4bD4cD54D55D56D5aD5bD64D65D6bD6cD72D73D74D75D7bD7cD7dD7eD82D83D84D85D8bD8cD8dD8eD94D95D9bD9cDa4Da5Da6DaaDabDb4Db5Db6Db7Db8Db9DbaDbbDbcDc3Dc4Dc7Dc8DccDd8De8CaaaD07D2dD32D76D86DdbDf7C333D27D33D43D59D5cD6aD9aDa9DacDc9DcbDd7CdddD00D01D02D03D04D05D0aD0bD0cD0dD0eD0fD10D11D12D1eD1fD20D21D2fD30D31D3fD40D41D4fD88Da0Da1DafDb0Db1DbfDc0Dc1DcfDd0Dd1DdfDe0De1DeeDefDf0Df1Df2Df3Df4Df5DfbDfcDfdDfeDffCdddD06D13D14D15D1aD1bD1cD1dD22D2eD50D51D52D5eD5fD60D77D78D79D87D89D90Da2DaeDdeDe2De3De4De5DeaDebDecDedDf6DfaC555D3aD66D96DcaCbbbD16D25D3eD67D6fD70D80D97D9fDe6C444D35D3dD58Da8Dc6C777D6dCbbbD09D5dDadDb2DddDf9C444D17D3bD57D71D81Da7Db3De7C666D19D2cD4dD7aD8aD9dDdcDe9CcccD2aDceDd5DdaC555D29D36D63D93Dc5DcdDd9C999D08D6eD9eDc2Df8CaaaD26D2bD42D61D91Dd6C666D7fD8fDbdC888Da3CcccD4eD68D98DbeDd2C999D69D99C777D24D53Dd3Dd4C888D23D62D92"{
	/*Dialog.create("RAZ");
	Dialog.addCheckbox("Remettre les parametres a zero ", true);
	Dialog.show();
	raz = Dialog.getCheckbox(); 
	print(raz);*/
	
	Dialog.create("raz");
	Dialog.addChoice("Remise a zero ",newArray("Oui", "Non"));
	Dialog.show();
	zero = Dialog.getChoice();
	
	if (zero=="Oui") {
			raz_variables();
			efface_images_temp();			
	}
	
	
	infos();
	
	print(plateau);
	print(NomVerificateur);
	print(NomMicroscope);
	print(TypeMicroscope);
	print(NomObjectif);
	print(NaObjectif);
	//Array.show(wave);
	//Array.show(wave_value);
	


}



//////////////////////////////////////////////////////////////////////Calibration//////////////////////////////////////

macro "Calibration Action Tool - C00dDd1DdeDeeDffC8aeD12C22cD93CdcfDb8C00dDc0CcceD1aD36Dd6C37fD30D41D50D61CfffD28D29D2aD2bD3aD3bD4aD4bD59D5aD5bD69D6aD6bD7aD7bD89D8aD8bD98D9aD9bDa9DaaDabDbaDbbDcaDcbDdbDebDfbC00eD0fD1eD2eD2fD3eD3fD4eD4fD5eD5fD6eD6fD7eD7fD8eD8fD9eD9fDaeDafDbeDbfDceDcfDd0DdfDefDf0Df1CbadD16C47dD21CfffDc7DfaC01dD91CddeD05Dd7Dd8Dd9C99cDe7CaadD25D45Dc5C33cD63CefeD01C00dDa1CccfD0cD1cD2cD3cD4cD5cD6cD7cD8cD9cDacDbcDccDdcDecC57dD23CbbeD55D65Da5Db5C66dD94Dd4CfffD79C22dDd2C99cD47De6CaacDc6De5C32dD0dCdeeD02C21dD2dD3dD4dD5dD6dD9dDadDbdDcdCcceD35Dd5C37fD60D70D80CbaeDa7C35fD90CfffD0bD1bD27D49D58D68Df8Df9C22dDc2DedC88eD74D84Db4CbbdD75C24eD81CeffD00D97C00eD1fDe0De1C77dDfdCcbeD78C67cD54C13dD72C99dD77C8aeD24C22dD1dCeefDdaDfcC21cDa3CcbeD06C47eD31CbbdD95C47eD42CfffD39D99Db9Dc8Dc9C11dD82C99cDe8CbadD26C45cD53CeefD38D57D67D88Df7C01eD0eC58eD13C66dDa4C22dDa2C99cD76De9C9beD14C22eDf2CeeeDf5Df6CcceD15CbbeD07D08D09C37fD40C21dDddC88eDc4CabeD11C44eDc3C68eD33CbbeDb7C77dDe4C12eDa0CaadD17D18D19C8aeD34C22dDd3De3CddeD37D87C11dD7dD8dDc1DfeCbbdD96C46eD20D62C99cD46C33dD73C57eD22C23dD92CcceD85D86CbbeDf4C36fD71CaaeDa6Db6C33eD83Db2C78dD44C66dD64C8aeD03CcceD56D66CbbdDeaC11dDe2C8adD04C44dDb3C57fD52C57eD43CabeD10C33eDb1CcceD0aDa8C36fD51C69fD32C77eDf3C01dDb0CcbdD48"{
	raz("calibration");
	origid=getImageID();
	dir=getInfo("image.directory");
	Stack.getDimensions(width, height, channels, slices, frames);
	getPixelSize(unit, pixelWidth, pixelHeight);
	run("Duplicate...", "title=calibration duplicate");
	if (channels>1)run("Hyperstack to Stack");
	if ((channels+slices)>2){
		run("Z Project...", "projection=[Max Intensity]");
		selectWindow("calibration");
		close();
		selectWindow("MAX_calibration");
		rename("calibration");
		}
	run("Gaussian Blur...", "sigma="+radius_calibration);//lissage
	run("8-bit");
	seuillage (seuil_calibration,0);
	//setAutoThreshold("Li dark");	
	run("Analyze Particles...", "size="+taille_calibration[0]+"-"+taille_calibration[1]+" pixel display clear add");//taille des points en pixels 
	run("Nnd ");
	selectWindow("Nearest Neighbor Distances"); //Calcul des plus proches voisins
	str = getInfo("window.contents");
	extraction = newArray(0);
	lines=split(str, "\n");   // split  d un fichier texte en lignes
	for(i=1; i<lines.length; i++) {		
		line = lines[i];				
		columns = split(line, "\t");	
		extract = columns[1];											
		extraction = Array.concat(extraction, extract);	
		}
	Array.getStatistics(extraction, min, max, mean, stdDev);
	
	//Array.show(extraction);
	selectImage(origid);
	// si l image n est pas calibree
	if (pixelWidth==1){
	calib=20/mean; // 20 = distance entre points
	run("Set Scale...", "distance=1 known="+calib+" pixel=1 unit=micron");	
		} 
	else{
		print ("Distance moyenne :"+mean);
		print ("Ecart type :"+stdDev);	
	}
	
}


macro 'Calibration Action Tool Options'  {
	Dialog.create("Calibration");
	Dialog.addNumber("Taille du filtre:", radius_calibration);
  	Dialog.addChoice("Seuillage:", newArray("Manuel","Default","Huang","Intermodes","IsoData","IJ_IsoData","Li","MaxEntropy","Mean","MinError","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen"),seuil_calibration[0]);
	Dialog.addSlider("Surface mini en px", 1, 10000, taille_calibration[0]);
	Dialog.addSlider("Surface maxi en px", taille_calibration[0], 10000, taille_calibration[1]);
	Dialog.addCheckbox("Valeurs par defaut", false);
	Dialog.show();  
	radius_calibration=Dialog.getNumber();	
    seuil_calibration[0]=Dialog.getChoice();
	taille_calibration[0]=Dialog.getNumber();
	taille_calibration[1]=Dialog.getNumber();	
	if(Dialog.getCheckbox()){
		seuil_calibration=newArray("Li",100,255);
		radius_calibration=1;
		taille_calibration=newArray(50,500);	
	}
	}



//////////////////////////////////////////////////////////////////////Homogeneite//////////////////////////////////////
macro "Homogeneite Action Tool - C00fD00D01D02D03D06D07D08D09D0cD0dD0eD0fD10D11D12D13D16D17D18D19D1cD1dD1eD1fD20D21D22D23D26D27D28D29D2cD2dD2eD2fD30D31D32D33D36D37D38D39D3cD3dD3eD3fD60D61D62D63D66D67D68D69D6cD6dD6eD6fD70D71D72D73D76D77D78D79D7cD7dD7eD7fD80D81D82D83D86D87D88D89D8cD8dD8eD8fD90D91D92D93D96D97D98D99D9cD9dD9eD9fDc0Dc1Dc2Dc3Dc6Dc7Dc8Dc9DccDcdDceDcfDd0Dd1Dd2Dd3Dd6Dd7Dd8Dd9DdcDddDdeDdfDe0De1De2De3De6De7De8De9DecDedDeeDefDf0Df1Df2Df3Df6Df7Df8Df9DfcDfdDfeDff"{
	raz("homogeneite");
	origid=getImageID();
	dir=getInfo("image.directory");
	Stack.getDimensions(width, height, channels, slices, frames);
	getPixelSize(unit, pixelWidth, pixelHeight);
	if (channels>1)exit ("Faire l analyse sur un canal");
	inter_point=20/pixelWidth; //Distance entre points
	run("Select None");
	run("Duplicate...", "title=homogeneite duplicate");
	
	if ((channels+slices)>2){
		run("Z Project...", "projection=[Max Intensity]");
		selectWindow("homogeneite");
		close();
		selectWindow("MAX_homogeneite");
		rename("homogeneite");
		}
	run("Gaussian Blur...", "sigma="+radius_homogeneite);//lissage
	if(croix){
		seuillage (seuil_homogeneite,0);
		//setAutoThreshold("Otsu dark");;
		n1=roiManager("count");
		run("Analyze Particles...", "size="+taille_homogeneite[0]+"-"+taille_homogeneite[1]+" add");//taille de la croix
		n2=roiManager("count");
		if (n2!=n1){ //si il y  une croix
			roiManager("Select", n2-1);
			run("Subtract...", "value=65000"); //efface
			roiManager("Delete");
		}
		run("Select None");
		resetThreshold();
	}
	run("Maximum...", "radius="+(inter_point*1.22)); // taille du filtre diagonale entre points
	run("Duplicate...", "title=temp duplicate");
	run("32-bit");
	getStatistics(area, mean, min, max, std, histogram);
	run("Divide...", "value="+max/100); //Normalisation en pourcentage
	getStatistics(area, mean, min, max, std, histogram);
	std_homo=std;
	close();
	selectWindow("homogeneite");
	run("Gaussian Blur...", "sigma="+(inter_point/2));// taille du filtre diagonale sur 2
			// 10 classes de valeurs
	run("32-bit");
	getStatistics(area, mean, min, max, std, histogram);
	run("Divide...", "value="+max); //--> valeurs de 0 a 1
	run("Multiply...", "value=10"); //--> valeurs de 0 a 10
	run("Conversions...", " ");
	run("8-bit");
	getStatistics(area, mean, min, max, std, histogram);
	print("Deviation standard normalisee de l homogeneite: "+std_homo); // calcul sur des valeurs de 1 a 10
	
	run("Multiply...", "value=25.5");
	run("Conversions...", "scale");	
	run("Size...", "width=800 height=800 constrain average interpolation=Bilinear");
	//print(dir_argoloj+"homogeneite.jpg");
	saveAs("Jpeg", dir_argoloj+"homogeneite.jpg");	

}


macro 'Homogeneite Action Tool Options'  {
	Dialog.create("Homogeneite");
	Dialog.addNumber("Taille du filtre:", radius_homogeneite);
  	Dialog.addChoice("Seuillage:", newArray("Manuel","Default","Huang","Intermodes","IsoData","IJ_IsoData","Li","MaxEntropy","Mean","MinError","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen"),seuil_homogeneite[0]);
	Dialog.addSlider("Surface de la croix mini", 1, 500, taille_homogeneite[0]);
	Dialog.addSlider("Surface de la croix maxi", taille_homogeneite[0], 500, taille_homogeneite[1]);
	Dialog.addCheckbox("Croix centrale a enlever", croix);
	Dialog.addCheckbox("Valeurs par defaut", false);
	Dialog.show();
	radius_homogeneite=Dialog.getNumber();    
    seuil_homogeneite[0]=Dialog.getChoice();
	taille_homogeneite[0]=Dialog.getNumber();
	taille_homogeneite[1]=Dialog.getNumber();
	croix=Dialog.getCheckbox();
	raz_value=Dialog.getCheckbox();
	if(raz_value){
		seuil_homogeneite=newArray("Otsu",100,255);
		radius_homogeneite=1;
		taille_homogeneite=newArray(20,100);
		croix=false;		
	}
   }


/////////////////////////////////////////////////////////////////////Coalignement///////////////////////////////////////


macro "Coalignement Action Tool - Cf00D24D25D26D33D34D35D36D37D42D43D44D45D46D47D52D53D54D55D56D62D63D64D65D73C00fD48D49D4aD57D58D59D5aD5bD66D67D68D69D6aD6bD6cD76D77D78D79D7aD7bD7cD86D87D88D89D8aD8bD8cD97D98D99D9aD9bDa8Da9DaaC4f0D74D75D83D84D85D92D93D94D95D96Da2Da3Da4Da5Da6Da7Db2Db3Db4Db5Db6Db7Db8Dc3Dc4Dc5Dc6Dc7Dd4Dd5Dd6CcccD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D27D28D29D2aD2bD2cD2dD2eD2fD30D31D32D38D39D3aD3bD3cD3dD3eD3fD40D41D4bD4cD4dD4eD4fD50D51D5cD5dD5eD5fD60D61D6dD6eD6fD70D71D72D7dD7eD7fD80D81D82D8dD8eD8fD90D91D9cD9dD9eD9fDa0Da1DabDacDadDaeDafDb0Db1Db9DbaDbbDbcDbdDbeDbfDc0Dc1Dc2Dc8Dc9DcaDcbDccDcdDceDcfDd0Dd1Dd2Dd3Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDff"{
	raz("temp");
	raz("MAX_temp");
	raz("coal_result");
	if (!Infos) infos();
	origid=getImageID();
	dir=getInfo("image.directory");
	Stack.getDimensions(width, height, channels, slices, frames);
	width_stack=width;
	height_stack=height;
	getVoxelSize(width, height, depth, unit);
	inter_point=20/width; //Distance entre points
	run("Duplicate...", "title=temp duplicate");
	run("Hyperstack to Stack");
	run("Z Project...", "projection=[Max Intensity]");
	run("Subtract Background...", "rolling=50");
	selectWindow("temp");
	close();
	selectWindow("MAX_temp");
	run("Median...", "radius=2");
	run("Subtract Background...", "rolling=50");
	seuillage (seuil_coalignement_tot,0);
	//setAutoThreshold("Default dark");
	run("Analyze Particles...", "size="+taille_coalignement[0]+"-"+taille_coalignement[1]+" add"); //selection des points
	selectWindow("MAX_temp");
	close();
	count=roiManager("count");
	Tab_result_total=newArray(0);
	Tab_ref_resolution=newArray(0);
	Tab_nom_couple=newArray(0);
	combinaisons=0;
	if(TypeMicroscope=="WideField") facteur=2;
	if(TypeMicroscope=="Confocal") facteur=1.4;
		for (i=0;i<channels-1;i++){
			for(j=i+1;j<channels;j++){
				combinaisons++;
				ref_a=(facteur*wave_value[i]/pow(NaObjectif,2))/1000;
				ref_b=(facteur*wave_value[j]/pow(NaObjectif,2))/1000;
				if (ref_a>ref_b) Tab_ref_resolution=Array.concat(Tab_ref_resolution,ref_a);
				else Tab_ref_resolution=Array.concat(Tab_ref_resolution,ref_b);	
				Tab_nom_couple=Array.concat(Tab_nom_couple,wave[i]+"-"+wave[j]);
				}
			}
		//Array.show(Tab_ref_resolution);
	//newImage("coal_result", "32-bit composite-mode ", width_stack, height_stack, 1 , combinaisons, 1);
	newImage("coal_result", "RGB color-mode ", width_stack, height_stack, 1 , combinaisons, 1);
	//boucle sur les selections de points
	for(k=0;k<count;k++){
		selectImage(origid);
		Tab_xm_tot=newArray(0); // tableaux de tous les resultats
		Tab_ym_tot=newArray(0);
		Tab_zm_tot=newArray(0);		
		roiManager("Select", k);
		List.setMeasurements;
		x_roi=List.getValue("XM")/width;
		y_roi=List.getValue("YM")/height;
				
		run("Enlarge...", "enlarge=4");// agrandit la selection pour dupliquer
		setBatchMode(true);		
		for (c=1;c<=channels;c++){
				selectImage(origid);
				Tab_xm=newArray(0); // tableaux des resultats par points
				Tab_ym=newArray(0);
				Tab_zm=newArray(0);
				run("Duplicate...", "title=ch"+c+" duplicate channels="+c);
				run("8-bit");
				run("Median...", "radius="+radius_coalignement);
				setSlice( nSlices/2);
				setAutoThreshold(seuil_coalignement[0]+" dark");
				/*
				if(slices>1) seuillage (seuil_coalignement,1);//setAutoThreshold("Default dark stack");
				else seuillage (seuil_coalignement,0);//setAutoThreshold("Default dark");*/
				getThreshold(lower, upper);
				taille_mini_vx=taille_coalignement[2]/(width*height*depth);
				run("3D object counter...", "threshold="+lower+" slice="+slices/2+" min.="+taille_mini_vx+" max.=14092848 statistics");
				
				// recuperation des data
				titre="Statistics for ch"+c;
				Tab_xm=tableau_fenetre_texte(titre,18);if(Tab_xm.length!=1)	exit ("Nombre d objets incorrect!");
				Tab_ym=tableau_fenetre_texte(titre,19);
				Tab_zm=tableau_fenetre_texte(titre,20);
				selectWindow("ch"+c);
				close();
				selectWindow(titre);
				run("Close"); 
				Tab_xm_tot= Array.concat(Tab_xm_tot,Tab_xm);
				Tab_ym_tot= Array.concat(Tab_ym_tot,Tab_ym);
				Tab_zm_tot= Array.concat(Tab_zm_tot,Tab_zm);	
			}
	
		if(Tab_xm_tot.length!=channels||Tab_ym_tot.length!=channels||Tab_zm_tot.length!=channels)	exit ("Pas le bon nombre d objets!");
		Tab_entete=newArray(0);	
		Tab_result=newArray(0);
		
		
		n=0;
		for (i=0;i<channels-1;i++){
			for(j=i+1;j<channels;j++){
			n++;
				//sqrt[(Xa-Xb)²+(Ya-Yb)²+(Za-Zb)²] calcul de la distance euclidienne
			x=	(Tab_xm_tot[i]-Tab_xm_tot[j])*width;
			y=	(Tab_ym_tot[i]-Tab_ym_tot[j])*height;
			z=	(Tab_zm_tot[i]-Tab_zm_tot[j])*depth;
				
			delta= sqrt(pow(x,2)+pow(y,2)+(pow(z,2)));
			selectWindow("coal_result");
			Stack.setSlice(n); 
			set_px=(delta/Tab_ref_resolution[n-1]);
			//print (Tab_ref_resolution[n-1]);
			//set_px=(delta/seuil_resolution);
			//set_px=parseInt(set_px);
			//print (set_px);
			if(set_px<=1) set_px=0;
			else if(set_px<0.4) set_px=1;
			else if(set_px<0.6) set_px=2;
			else if(set_px<0.8) set_px=3;
			else if(set_px<1) set_px=4;
			else if(set_px<1.2) set_px=5;
			else if(set_px<1.4) set_px=6;
			else if(set_px<1.6) set_px=7;
			else if(set_px<1.8) set_px=8;
			else  set_px=9;
			
			
			r=256*256*255;
			v=256*255;
			b=255;
			tab_color=newArray(v,r+256*255,r+256*210,r+256*180,r+256*150,r+256*120,r+256*90,r+256*60,r+256*30,r);
			set_px=tab_color[set_px];
			setPixel(x_roi, y_roi, set_px);
			//setMetadata("Label", "Delta C"+i+1+" C"+j+1);
			//Tab_entete=Array.concat(Tab_entete,"Delta C"+i+1+" C"+j+1);
			setMetadata("Label", Tab_nom_couple[n-1]);
			Tab_entete=Array.concat(Tab_entete,Tab_nom_couple[n-1]);
			
			Tab_result=Array.concat(Tab_result,delta);
			Tab_result_total=Array.concat(Tab_result_total,delta);
			}
			}
			// Tableau de resultat
			if( !isOpen("Resultats Argolight")){
				run("Table...", "name="+title2+" width=1400 height=600");
				result="\\Headings: Roi          ";
				for (h=0; h<Tab_entete.length; h++){
					result=result+" \t"+Tab_entete[h];
					}
				print(fe, result);
		}
		result=""+(k+1);
		for (r=0; r<Tab_result.length; r++){
				result=result+" \t"+Tab_result[r];
			}
		print(fe,result);
	} // fin de boucle sur les roi	
	selectWindow("coal_result");
	run("Maximum...", "radius="+(inter_point/2)+" stack");
	
	
	
		Tab_result_moy=newArray(Tab_result.length);
		
		result="";
		for (i=1; i<Tab_result_moy.length; i++){
				result=result+" \t"+"";
			}
		print(fe,result);
		
		//stat		
		for (i=0; i<Tab_result.length; i++){
			moy=0;
			for (j=0; j<Tab_result_total.length; j=j+Tab_result.length){
				moy=moy+Tab_result_total[j+i];			
			}
			moy=moy/count;
			Tab_result_moy[i]=moy;			
		}
		result="Moyenne";
		for (i=0; i<Tab_result_moy.length; i++){
			result=result+" \t"+Tab_result_moy[i];
			}
		print(fe,result);	
		result="Reference";
		for (i=0; i<Tab_result_moy.length; i++){
			result=result+" \t"+Tab_ref_resolution[i];
			}
		print(fe,result);			
		
		
		setBatchMode(false);
		selectWindow("coal_result");
		//run("Duplicate...", "title=temp duplicate");
		run("Size...", "width=300 height=300 constrain average interpolation=Bilinear");
		run("Make Montage...", "columns=n rows=1 scale=1 label");
		
		run("Size...", "width=1800 height=300 constrain average interpolation=Bilinear");
	//print(dir_argoloj+"homogeneite.jpg");
	saveAs("Jpeg", dir_argoloj+"coalignement.jpg");
	selectWindow("coal_result");
	close();	

		
		
		run("Select None");
	
	
}

macro 'Coalignement Action Tool Options'  {
	Dialog.create("Coalignement");
	Dialog.addNumber("Taille du filtre:", radius_coalignement);
  	Dialog.addChoice("Seuillage:", newArray("Default","Huang","Intermodes","IsoData","IJ_IsoData","Li","MaxEntropy","Mean","MinError","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen"),seuil_coalignement[0]);
	Dialog.addSlider("Surface mini", 0.1, 50, taille_coalignement[0]);
	Dialog.addSlider("Surface maxi", taille_coalignement[0], 50, taille_coalignement[1]);
	Dialog.addSlider("Volume 3D mini(µm3)", 0.01, 10, taille_coalignement[2]);
	Dialog.addCheckbox("Valeurs par defaut", false);
	Dialog.show();
	radius_coalignement=Dialog.getNumber();    
    seuil_coalignement[0]=Dialog.getChoice();
	taille_coalignement[0]=Dialog.getNumber();
	taille_coalignement[1]=Dialog.getNumber();	
	taille_coalignement[2]=Dialog.getNumber();
defaut_value=	Dialog.getCheckbox();
	if(defaut_value){
		seuil_coalignement_tot=newArray("Default",100,255);
		seuil_coalignement=newArray("Yen",100,255,1);
		radius_coalignement=2;
		taille_coalignement=newArray(0.5,10,1);		
	}
   }

////////////////////////////////////////////////////////////////Dispersion////////////////////////////////////////////



macro "Dispersion Action Tool - C001D0eD0fDffC4f1Dc9C006D22Da0De3Df9C6f0D54Da4C004D04D0aD21D30D5fDbfDe2DedDf3DfbC3f7D28C002D1eCfa0D97C003D02D0bD0cD11D1dD4fDd0C4f4D3aC008Df7Df8C8f0D94Db7C005D1cD2dD31Dc1C1abD4dDe8C16cDaeCaf0D46D65D7aD9aC002D01D10D1fD2fD3fDdfDe0De1DeeDf2DfcC3f2D44C007D07D1bD60C7f0D45D73D83C004D3eDdeC3f8D26C05bDa1Caf0D49D4aD55D59D5aD5bD6bD75Da9C003D03D12D20D2eDc0DcfDd1C3f6Dd9C029De4C9f0D64D6aD74D7bD7cD85D8bD9bC006D6fDafC2bbDbdC29bDe7Cbf0D48C002D00D0dDefDf0Df1DfdDfeC5f0Db5C006D08DceDecC7f0Da5DabC3e8D5dC03aDd3Cbf0D8aC3f5D82C018Db1C9f0D84D8cDa6DaaC006D06D09D13D50Dd2Df5C29cD71C18bDe9C3f2D53Db4C017D7fDddC6f1D5cC005D05D40Db0Df4DfaC2daD52C16bDdcCbf0Da7Da8C4f6D4cD6dC039D1aDebC2adDd4Cf53D87Cbf0D47D56C4f1DacC6f0DbaC3e8DadC03aD15Cec0D86C4f4D7dD9dC018D70D80D9fC8f0D6cD95C1abD7eC18cD17D18C4f2DbbC007D90C7f1D37D38C2f9DdaC06cD19C3f6D27D92Dd6C039D3dC2cbD2aCf90D98Cbf0D58C5f1D63C007Df6C2f8Dc4C05bDcdDe5Cec0D68C3f5DcbDd7C019D41C1acD33D3cC19cD42C4f2Da3C017D14C6f2D39C2eaDa2C18bD6eC02aD2cC2ccDdbCf53D78Ccf0D57C4f1Dc6Dc8C7f0Db6Db8Db9C3f7D62Db3C04aD5eCeb0D79C3f4D35C17cD24C3f2DcaC16bD16DeaC3f6D72C029DbeC28cD91C5f0Dc7C3e8D29D3bC04bD51Cce0D96C3f5DbcDd8C18cDe6C3f3Dc5C3daDccC17cD2bC2bcDc3Cf53D77C03aDc2Cfc0D67C8f0D9cC29cD81D9eC5f1D36C2e9D34C029D32C2bbD8eCf91D89C6f1D93Ced0D99C19cDb2C4f3D8dC017D4eD8fC7f1D4bC2eaDd5C07cD61C02aD23C2ccD25Cf35D88C3f6D43Ccf0D69Cec0D76Cde0D66"{
	
	origid=getImageID();
	raz("dispersion");
	
	if (isOpen("Objects map of dispersion")){
		selectWindow("Objects map of dispersion");
		close();
		}
	dir=getInfo("image.directory");
	Stack.getDimensions(width, height, channels, slices, frames);
	getVoxelSize(width, height, depth, unit);
	if (channels>1)exit ("Faire l analyse sur un canal");
	run("Duplicate...", "title=dispersion duplicate");
	run("8-bit");
	//setSlice(slices/2);	
	run("Fire");
	run("Median...", "radius="+radius_dispersion+" stack");
	run("Enhance Contrast...", "saturated=0.3 normalize process_all use");
	seuillage (seuil_dispersion,1);
	//setAutoThreshold("Minimum dark stack");
	getThreshold(lower, upper);
	run("3D object counter...", "threshold="+lower+" slice="+slices/2+" min.=100 max.=14092848 objects statistics");
	selectWindow("Objects map of dispersion");
	run("Multiply...", "value=10 stack");
	setSlice(slices/2);	
	//recup data
	
	extraction = newArray(0);
	//extraction=tableau_fenetre_texte("Statistics for dispersion",20);
	selectWindow("Statistics for dispersion");
	str = getInfo("window.contents");
	lines=split(str, "\n");   // split  d un fichier texte en lignes
	if (lines.length==23){     //22 + label
		for(i=0; i<lines.length; i++) {		
			line = lines[i];				
			columns = split(line, "\t");	
			extract = parseInt(columns[20]);											
			extraction = Array.concat(extraction, extract);	
			}
		Array.getStatistics(extraction, min, max, mean, stdDev);
		distance_z_dispersion=(max-min)*depth;
		print("Distance entre z: "+distance_z_dispersion+" "+unit);
	} 
	else exit ("Il n y a pas les 22 objets!");
	selectWindow("Statistics for dispersion");
	
	run("Close"); 
	selectWindow("dispersion");
	setSlice(slices/2);
	run("Find Maxima...", "noise=1000 output=[Point Selection]");
	getSelectionCoordinates(xpoints, ypoints);
	x=xpoints[0];
	y=ypoints[0];
	run("Orthogonal Views");
	Stack.setOrthoViews(x, y, slices/2);
	selectWindow("XZ "+y);
	saveAs("Jpeg", dir_argoloj+"dispersion.jpg");
	//Stack.stopOrthoViews;
}

macro 'Dispersion Action Tool Options'  {
	Dialog.create("Dispersion");
	Dialog.addNumber("Taille du filtre:", radius_dispersion);
  	Dialog.addChoice("Seuillage:", newArray("Manuel","Default","Huang","Intermodes","IsoData","IJ_IsoData","Li","MaxEntropy","Mean","MinError","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen"),seuil_dispersion[0]);
	Dialog.show();  
	radius_dispersion=Dialog.getNumber();	
    seuil_dispersion[0]=Dialog.getChoice();
   }

////////////////////////////////////////////////////////////////Resolution////////////////////////////////////////////



macro "Resolution Action Tool - C00fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D24D25D26D27D28D29D2aD2bD2cD2dD2eD2fD50D51D52D53D54D55D56D57D58D59D5aD5bD5cD5dD5eD5fD60D61D62D63D64D65D66D67D68D69D6aD6bD6cD6dD6eD6fD90D91D92D93D94D95D96D97D98D99D9aD9bD9cD9dD9eD9fDa0Da1Da2Da3Da4Da5Da6Da7Da8Da9DaaDabDacDadDaeDafDd0Dd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefCcccD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD30D31D32D33D34D35D36D37D38D39D3aD3bD3cD3dD3eD3fD40D41D42D43D44D45D46D47D48D49D4aD4bD4cD4dD4eD4fD70D71D72D73D74D75D76D77D78D79D7aD7bD7cD7dD7eD7fD80D81D82D83D84D85D86D87D88D89D8aD8bD8cD8dD8eD8fDb0Db1Db2Db3Db4Db5Db6Db7Db8Db9DbaDbbDbcDbdDbeDbfDc0Dc1Dc2Dc3Dc4Dc5Dc6Dc7Dc8Dc9DcaDcbDccDcdDceDcfDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDff"{
	raz("resolution");
	origid=getImageID();
	dir=getInfo("image.directory");
	Stack.getDimensions(width, height, channels, slices, frames);
	//Choix de la slice la plus intense	
	index_slice=1;
	index_channel=1;
	int_max=0;
		for (i=1;i<=slices;i++){
			for(j=1;j<=channels;j++){
				Stack.setSlice(i);
				Stack.setChannel(j);
				getStatistics(area, mean, min, max, std, histogram);
				if (mean>int_max) {
					int_max=mean;
					index_slice=i;
					index_channel=j;
					}					
			}			
		}	
	Stack.setSlice(index_slice);
	Stack.setChannel(index_channel);
	run("Plots...", "width="+width+120+" height=400 font=12 draw draw_ticks minimum=0 maximum=0 interpolate");
	run("Duplicate...", "title=resolution");
	run("Duplicate...", "title=temp");// pour redresser l image trouve l angle de rotation
	run("Median...", "radius="+radius_resolution);
	seuillage (seuil_resolution,0);
	//setAutoThreshold("Default dark");
	run("Analyze Particles...", "size="+taille_resolution[0]+"-"+taille_resolution[1]+" add");
	count=roiManager("count");
	roiManager("Select", count-1);
	List.setMeasurements;
	angle=List.getValue("FeretAngle");
	milieu=List.getValue("Y");
	roiManager("Deselect");
	roiManager("Delete");
	close();
	// PlotProfile sur l originale apres rotation
	selectWindow("resolution");
	run("Rotate... ", "angle="+(angle-90)+" grid=1 interpolation=Bilinear"); //rotation
	run("Specify...", "width="+width+" height="+height/2+" x=0 y="+(milieu+200));
	profile = getProfile();
	run("Plot Profile");
	/*newImage("listplotprofile", "16-bit black", profile.length, 1, 1);
	for (i=0; i<profile.length; i++){
		setPixel(i, 0,  profile[i]);
	}
	selectWindow("listplotprofile");
	run("Find Maxima...", "noise=20 output=[Point Selection]");*/
	selectWindow("Plot of resolution");
	saveAs("Jpeg", dir_argoloj+"resolution.jpg");
	maxima=Array.findMaxima(profile, radius_resolution);
	bande_resolution=maxima.length-30;
	bande_resolution=validation_nombre("Nombre de bandes resolues :",bande_resolution,0,10);
	print("Nombre de bandes resolues ="+bande_resolution); //4 maxima pour une bande resolue (10 bandes)
}

macro 'Resolution Action Tool Options'  {
	Dialog.create("Resolution");
	Dialog.addNumber("Taille du filtre:", radius_resolution);
  	Dialog.addChoice("Seuillage:", newArray("Manuel","Default","Huang","Intermodes","IsoData","IJ_IsoData","Li","MaxEntropy","Mean","MinError","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen"),seuil_resolution[0]);
	Dialog.addSlider("Surface mini", 1, 20000, taille_resolution[0]);
	Dialog.addSlider("Surface maxi", taille_resolution[0], 20000, taille_resolution[1]);
	Dialog.addCheckbox("Valeurs par defaut", false);
	Dialog.show();   
	radius_resolution=Dialog.getNumber();	
    seuil_resolution[0]=Dialog.getChoice();
	taille_resolution[0]=Dialog.getNumber();
	taille_resolution[1]=Dialog.getNumber();	
	if(Dialog.getCheckbox()){
		seuil_resolution=newArray("Default",100,255);
		radius_resolution=20;
		taille_resolution=newArray(2,10000);
	}
   }

////////////////////////////////////////////////////////////////Linearite////////////////////////////////////////////


macro "Linearite Action Tool - C00fD99D9aD9bD9cD9dD9eD9fDa9DaaDabDacDadDaeDafDb9DbaDbbDbcDbdDbeDbfDc9DcaDcbDccDcdDceDcfDd9DdaDdbDdcDddDdeDdfDe9DeaDebDecDedDeeDefDf9DfaDfbDfcDfdDfeDffC0ffD00D01D02D03D04D05D06D10D11D12D13D14D15D16D20D21D22D23D24D25D26D30D31D32D33D34D35D36D40D41D42D43D44D45D46D50D51D52D53D54D55D56D60D61D62D63D64D65D66C09fD90D91D92D93D94D95D96Da0Da1Da2Da3Da4Da5Da6Db0Db1Db2Db3Db4Db5Db6Dc0Dc1Dc2Dc3Dc4Dc5Dc6Dd0Dd1Dd2Dd3Dd4Dd5Dd6De0De1De2De3De4De5De6Df0Df1Df2Df3Df4Df5Df6CcccD07D08D17D18D27D28D37D38D47D48D57D58D67D68D70D71D72D73D74D75D76D77D78D79D7aD7bD7cD7dD7eD7fD80D81D82D83D84D85D86D87D88D89D8aD8bD8cD8dD8eD8fD97D98Da7Da8Db7Db8Dc7Dc8Dd7Dd8De7De8Df7Df8C03fD09D0aD0bD0cD0dD0eD0fD19D1aD1bD1cD1dD1eD1fD29D2aD2bD2cD2dD2eD2fD39D3aD3bD3cD3dD3eD3fD49D4aD4bD4cD4dD4eD4fD59D5aD5bD5cD5dD5eD5fD69D6aD6bD6cD6dD6eD6f"{
	raz("linearite");
	origid=getImageID();
	dir=getInfo("image.directory");
	Stack.getDimensions(width, height, channels, slices, frames);
	run("Duplicate...", "title=linearite");
	run("8-bit");
	run("Median...", "radius="+radius_linearite);
	run("Auto Local Threshold...", "method=Sauvola radius=15 parameter_1=0 parameter_2=0 white"); //calcul seuillage local
	//seuillage (seuil_linearite,0);
	setAutoThreshold("Default dark");
	run("Analyze Particles...", "size="+taille_linearite[0]+"-"+taille_linearite[1]+" add");
	close();
	selectImage(origid);
	roiManager("Show All");
	run("Fire");
	run("Enhance Contrast...", "saturated=0");
	waitForUser("Selections","Correction des selections");
	setTool("rectangle");
	count=roiManager("count");	
	if (count==16){
	Roi_sort(4,4);
	Roi_fit_16();	
	}
	else if(count==32){
	roiManager("Select", count-1);
	List.setMeasurements;
	angle=List.getValue("FeretAngle");
	roiManager("Deselect");
	if (angle>45&&angle<135) Roi_sort(2,16); //en fonction du l orientation de l image
	else Roi_sort(16,2);
	Roi_fit_32();	
	}
	else {
	exit ("Nombre de Roi incorrect !");	
	}

}

macro 'Linearite Action Tool Options'  {
	Dialog.create("Linearite");
	Dialog.addNumber("Taille du filtre:", radius_linearite);
  	Dialog.addSlider("Surface mini", 1, 100000, taille_linearite[0]);
	Dialog.addSlider("Surface maxi", taille_linearite[0], 100000, taille_linearite[1]);
	Dialog.addCheckbox("Valeurs par defaut", false);
	Dialog.show(); 
	radius_linearite=Dialog.getNumber();	
    taille_linearite[0]=Dialog.getNumber();
	taille_linearite[1]=Dialog.getNumber();	
	if(Dialog.getCheckbox()){
		radius_linearite=2;
		taille_linearite=newArray(200,1000000);
	}
   }


////////////////////////////////////////Geometrie ////////////////////////////////////////////////////////////////////
macro "Geometrie Action Tool - C44fD7aDa7CddeD41Da5DbdDc5Dd3De5C88eD26D52Da6DacCeeeD00D01D02D03D04D05D06D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D1bD1cD1dD1eD1fD20D21D22D2cD2dD2eD2fD30D31D36D3dD3eD3fD40D44D45D49D4aD4eD4fD50D53D54D5bD5eD5fD60D63D6bD6fD90D93D9bD9fDa0Da3Da4DaaDabDaeDafDb0Db4Db5Db6Db9DbaDbeDbfDc0Dc1Dc6DcdDceDcfDd0Dd1Dd2DdcDddDdeDdfDe0De1De2De3De4DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df9DfaDfbDfcDfdDfeDffC77fD2aD61D69D72D91D97CddeD1aD35D39D46D5aD6eD94D9eDb1Dc9CcceD23C66fD25D28D29D42D56D6dD87D8aD95D9dDb2CaaeD07D48D82D8cD8eDb3C88eD37D89D98Db7Dc7Dd4CcceD4dD64D6cDc2DdbDe9C66fD33D3bD4cD65D74D78D79D99Da8DbcDc3DcbDd5Dd8Dd9De7C99eD51DadDcaC77fD24D47D70D7cD7eDd6CcceD08D15D19D2bD8fD9cDccDf8CbbeD38D3aD3cD43D55D80D83D86D8bDb8DbbDc8De6C88fD18D67D73D76D7bDa2DdaC55fD81D8dDd7C99eD59D5cD5dD7fD84D88D96Dc4De8CbbeD32D4bD62D66D92D9aDa9CaaeD16D34D68D6aDa1Df7C44fD58D75D77C55fD17D27D85C33fD57D71D7d"{

	origid=getImageID();
	dir=getInfo("image.directory");
	if (roiManager("count")>0){
			roiManager("Deselect");
			roiManager("Delete");
	}
	run("Select None");
	Stack.getDimensions(width, height, channels, slices, frames);
	getPixelSize(unit, pixelWidth, pixelHeight);
	if (slices==1) slices=frames;
	seuillage (seuil_geometrie,1);
	//setAutoThreshold("Default dark stack");
	Tab_xm=newArray(slices);
	Tab_ym=newArray(slices);
	setBatchMode(true);
	for (i=1; i<=slices; i++) {
		setSlice(i);
		run("Create Selection");
		List.setMeasurements;
		Tab_xm[i-1]=List.getValue("X");
		Tab_ym[i-1]=List.getValue("Y");
		run("Select None");
	}
	resetThreshold();
	//Array.show(Tab_xm);
	//Array.show(Tab_ym);
	
	Tab_entete=newArray(0);	
	Tab_result=newArray(0);
	total=0;
	for (i=1;i<slices;i++){
					delta= sqrt(pow((Tab_xm[i]-Tab_xm[i-1]),2)+pow((Tab_ym[i]-Tab_ym[i-1]),2));
					total=total+delta;
					print ("Decalage "+i+"-"+i+1+" :"+delta+" "+unit);

	
	
	}
	setBatchMode(false);
	decalage_geometrie=total/(slices-1);
	print ("Decalage Moyen : "+decalage_geometrie+" "+unit);
	selectImage (origid);
	setSlice(1);
	run("ROI Manager...");
	for (i=0;i<slices;i++){
		makePoint(Tab_xm[i]/pixelWidth, Tab_ym[i]/pixelWidth);
		//run("Specify...", "width=1 height=1 x="+Tab_xm[i]+" y="+Tab_ym[i]+" slice=1 scaled");
		roiManager("Add");	
	}
	roiManager("Show All");
	
	
	
	

}

macro 'Geometrie Action Tool Options'  {
	Dialog.create("Geometrie");
  	Dialog.addChoice("Seuillage:", newArray("Manuel","Default","Huang","Intermodes","IsoData","IJ_IsoData","Li","MaxEntropy","Mean","MinError","Minimum","Moments","Otsu","Percentile","RenyiEntropy","Shanbhag","Triangle","Yen"),seuil_geometrie[0]);
	Dialog.show();    
    seuil_geometrie[0]=Dialog.getChoice();
	
   }
   
////////////////////////////////////////////////////////////////PDF////////////////////////////////////////////
   
   
 macro "Rapport_pdf Action Tool - C200DcdDdcDddDebDecDedC400DeeC311D38Db1Dc1C422D12D58C310De1Cc00DcfDdfDefC321D15D16D25D26D34D35D43D44D52D54D7aC655D72C210D1dD6cD7bD8bDc7Dd6De2Cb10DfeC311D17D18D27D28D2aD36D37D45D46D47D49D4dD55D56D57D81D91D92D93D94Da1DdaC655D63D64D67D6aD83D85Db9C311D19D1aD29D39D48D8aD95D96D9aDa2Da3Da4Db2Db3Cd00DffC322D14D24D33D42D97C876Da9C200D5dD6dD7cD7dD8cD8dD9bD9cD9dDabDacDbbDbcDcbDd7De3De4De5De6De7De8Cb11Df9DfaDfbDfcDfdC311D2dC533D2cDaaC311D1bD1cD6bDa5Da6Da7Db4Db5Db6Db7Dc2Dc3Dc4Dc5Dc6Dd1Dd2Dd3Dd4Cc11De0C421D51D61D71C766D3bD99DbaCb21D0bD0cD0dCd21D30D40D50D60D70D80Cd21D90Da0C422D13D22D23D31D32D41D53Ca99D73D76C200DbdDccDeaC510D1eD2eD3eD4eD5eD6eD7eC422Dd9C211Dd5Dd8Cc10D1fD2fD3fD4fD5fD6fD7fD8fD9fDafDbfC755D3cD59D84D87DcaCb11D0eDf1Df2Df3Df4Df5Df6Df7Cc31D01D02Cc21Db0Dc0Dd0C877D78C644D65D86D89Da8Db8Cb21D05D06D07C766D98Cd31D10D20Ce20D00CbaaD75D77C500D8eD9eDaeDbeDceDdeC422D3dD5cCb11Df8Ce10D0fC877D4bD4cD5bDc9C543D3aD66Cb21D08D09D0aCaa9D74C200DadDdbDe9C433Dc8C988D68Cc21D03D04C866D4aD79D88CbaaD5aC422D11D21C432D82Ce10Df0C544D2bC533D62CcbbD69"{
 run("Close All");
 dir_rapport=getDirectory("Repertoire de sauvegarde du rapport");
 //////////////////////renseignements/////////

if (!Infos) infos();

 /////////////////////////////////////////////// 
 
 
new_pdf_avec_entete(1);

image_dans_pdf(dir_argoloj,"homogeneite.jpg",700,540,800);
image_dans_pdf(dir_argoloj,"coalignement.jpg",200,1720,1800);
image_dans_pdf(dir_argoloj,"dispersion.jpg",600,2400,1000);

setColor(0, 0, 0);
setFont("SansSerif", 40, "bold");
drawString("Homogénéité", 200, 400);
drawString("Coalignement", 200, 1500);
drawString("Dispersion", 200, 2400);

 /*Array.show(Tab_entete);
 Array.show(Tab_result_moy);*/

setFont("SansSerif", 36);
drawString("Déviation standard normalisée de l'homogénéité: "+std_homo, 200, 480);
drawString("Cannaux", 200, 1580);
drawString("Ecarts"+"("+unit+")", 200, 1640);
drawString("Ref theorique en z", 200, 1700);
for(i=0;i<Tab_entete.length;i++){
	drawString(Tab_entete[i], 200+((i+1)*300), 1580);
	drawString(Tab_result_moy[i], 200+((i+1)*300), 1640);
	drawString(Tab_ref_resolution[i]/1000, 200+((i+1)*300), 1700);
}
/*
for(i=0;i<Tab_entete.length;i++){
	drawString(Tab_entete[i], 200+((i+1)*300), 1580);
	drawString(Tab_result_moy[i]+" "+unit, 200+((i+1)*300), 1640);
	drawString(Tab_ref_resolution[i]+" "+unit, 200+((i+1)*300), 1700);
}
*/
drawString("Distance entre z :"+distance_z_dispersion, 200, 2480);

new_pdf_avec_entete(2);

image_dans_pdf(dir_argoloj,"resolution.jpg",100,520,1800);
image_dans_pdf(dir_argoloj,"linearite16.jpg",100,1350,1800);
image_dans_pdf(dir_argoloj,"linearite32.jpg",100,2080,1800);
setColor(0, 0, 0);
setFont("SansSerif", 40, "bold");
drawString("Résolution", 200, 400);
drawString("Linéarité 16", 200, 1330);
drawString("Linéarité 32", 200, 2080);
drawString("Géometrie", 200, 2830);
setFont("SansSerif", 36);
drawString("Nombre de bandes résolues: "+bande_resolution, 200, 480);
drawString("Decalage Moyen : "+decalage_geometrie+" "+unit, 200, 2930);


fichier_pdf=plateau+datef+"_"+NomMicroscope+"-Obj"+NomObjectif+"_"+"argo_rapport.pdf";

run("PDF Writer", "scale save one save=["+dir_rapport+fichier_pdf+"]");
close();
close();
exec ("open",dir_rapport+fichier_pdf);

}

macro "About These Macros Action Tool - C000C111C222C333C444C555D54D55D63D73D7cD7dD83D88D89D8aD8cD8dD93D97Da3Da4Da5Da6C555D7aDb5C555D72D82C555D45Da7C555D98C555D53C555D79C555C666Db4C666Db6C666C777D92C777D87C777D64C777D62C777C888D44C888D96C888D78C999D94C999CaaaCbbbD99Db3CbbbDa2CbbbDa8CbbbCcccDb7CcccD52CcccD43CcccD65CcccCdddD74CdddD77CdddD86CdddD84D95CdddD00D01D02D03D04D05D06D07D08D09D0aD0bD0cD0dD0eD0fD10D11D12D13D14D15D16D17D18D19D1aD1bD1cD1dD1eD1fD20D21D22D23D24D25D26D27D28D29D2aD2bD2cD2dD2eD2fD30D31D32D33D34D35D36D37D38D39D3aD3bD3cD3dD3eD3fD40D41D42D46D47D48D49D4aD4bD4cD4dD4eD4fD50D51D56D57D58D59D5aD5bD5cD5dD5eD5fD60D61D66D67D68D69D6aD6bD6cD6dD6eD6fD70D71D75D76D7bD7eD7fD80D81D85D8bD8eD8fD90D91D9aD9bD9cD9dD9eD9fDa0Da1Da9DaaDabDacDadDaeDafDb0Db1Db2Db8Db9DbaDbbDbcDbdDbeDbfDc0Dc1Dc2Dc3Dc4Dc5Dc6Dc7Dc8Dc9DcaDcbDccDcdDceDcfDd0Dd1Dd2Dd3Dd4Dd5Dd6Dd7Dd8Dd9DdaDdbDdcDddDdeDdfDe0De1De2De3De4De5De6De7De8De9DeaDebDecDedDeeDefDf0Df1Df2Df3Df4Df5Df6Df7Df8Df9DfaDfbDfcDfdDfeDff"{
open(getDirectory("macros")+"toolsets/MRI_ArgoloJ_toolset.txt");
}









///////////////////////////////////////////////Fonctions /////////////////////////////////////////////////////////////

function Roi_sort(ligne,colonne){
roiManager("Deselect");
count=roiManager("count");
if ((ligne*colonne)!=count) exit("Nombre de selections incorrect !");
   for (j=0; j<ligne; j++) { 	
      for (i=0; i<colonne; i++) {
          roiManager("Select", (j*colonne)+i);
		   getSelectionBounds(x, y, width, height);
		  roiManager("Rename", IJ.pad(j, 4)+"-"+IJ.pad(x, 4));
		  roiManager("Deselect");
          }		  
      }	  
		roiManager("Sort");
		roiManager("UseNames", "true");
		roiManager("UseNames", "false");
		roiManager("Show All");
}


function Roi_fit_16(){
	nom=getTitle();
	 roi_index = newArray(16);
	 roi_mean = newArray(16);
	 roiManager("Select", 15);
	 getStatistics(area, mean);
	 mean_min=mean; 
	for (i=0; i<16; i++) {
		roiManager("Select", i);
		roi_index[i]=i;
		getStatistics(area, mean);
		mean=mean/mean_min;
		roi_mean[i]=mean;
	}
	run("Select None");
	Array.sort(roi_mean);
	Fit.doFit("Straight Line", roi_index, Argo_intensites_16);
	Fit.plot();
	rename("Fit etalonnage");
	print("Valeurs specifique a la lame: "+"a="+d2s(Fit.p(0),6)+", b="+d2s(Fit.p(1),6), ", R^2="+Fit.rSquared);
			  
	Fit.doFit("Straight Line", roi_index, roi_mean);
	Fit.plot();
	rename("Fit "+nom);
	saveAs("Jpeg", dir_argoloj+"linearite16.jpg");
	print("a="+d2s(Fit.p(0),6)+", b="+d2s(Fit.p(1),6), ", R^2="+Fit.rSquared); 
	  
}

function Roi_fit_32(){
	 	nom=getTitle();
	 roi_index_d = newArray(16);
	 roi_mean_d = newArray(16);
	 roi_index_m = newArray(16);
	 roi_mean_m = newArray(16);
	 roiManager("Select", 0);
	 getStatistics(area, mean);
	 mean_min_d=mean; 
	 roiManager("Select", 31);
	 getStatistics(area, mean);
	 mean_min_m=mean; 
	for (i=0; i<16; i++) {
		roiManager("Select", 2*i);
		roi_index_d[i]=i;
		getStatistics(area, mean);
		mean=mean/mean_min_d;
		roi_mean_d[i]=mean;
		roiManager("Select", (2*i)+1);
		roi_index_m[i]=i;
		getStatistics(area, mean);
		mean=mean/mean_min_m;
		roi_mean_m[i]=mean;
	}
	run("Select None");
		
	Array.sort(roi_mean_d);
	Array.sort(roi_mean_m);
	 
	Fit.doFit("Straight Line", roi_index_d, Argo_intensites_32);
	  Fit.plot();
	  rename("Fit etalonnage");
	  print("Valeurs specifique a la lame: "+"a="+d2s(Fit.p(0),6)+", b="+d2s(Fit.p(1),6), ", R^2="+Fit.rSquared);

	  Fit.doFit("Straight Line", roi_index_d, roi_mean_d);
	  Fit.plot();
	  rename("Fit "+nom+"_1");
	  print("Valeurs premiere colonne: "+"a="+d2s(Fit.p(0),6)+", b="+d2s(Fit.p(1),6), ", R^2="+Fit.rSquared);
	  Fit.doFit("Straight Line", roi_index_m, roi_mean_m);
	  Fit.plot();
	  rename("Fit "+nom+"_2");
	  saveAs("Jpeg", dir_argoloj+"linearite32.jpg");
	  print("Valeurs deuxieme colonne: "+"a="+d2s(Fit.p(0),6)+", b="+d2s(Fit.p(1),6), ", R^2="+Fit.rSquared);
	  
}

//remise a zero
function raz(titre){
	run("Clear Results");
	origid=getImageID();
	if (isOpen(titre)){
		selectWindow(titre);
		close();
	}
	if (roiManager("count")>0){
			roiManager("Deselect");
			roiManager("Delete");
	}
	selectImage(origid);
	run("Select None");
}


function tableau_fenetre_texte(titre,colonne){
	selectWindow(titre);
	str = getInfo("window.contents");
	extraction = newArray(0);
	lines=split(str, "\n");   // split  d un fichier texte en lignes
	for(i=1; i<lines.length; i++) {		
		line = lines[i];				
		columns = split(line, "\t");	
		extract = parseFloat(columns[colonne]);											
		extraction = Array.concat(extraction, extract);
		}
		return extraction;
	}

	
function seuillage (seuils,stack){
	if(seuils[0]=="Manuel"){
		run("Threshold...");
		waitForUser("Seuillage","Reglage du Seuil");
		getThreshold(lower, upper);
		seuils[1]=lower;
		seuils[2]=upper;	
	} else {	
		if(stack==1){
			setSlice( nSlices/2);
			setAutoThreshold(seuils[0]+" dark");		
		}
		if(stack==0) setAutoThreshold(seuils[0]+" dark");	
	}		
}


function seuillage (seuils,stack){
	if(seuils[0]=="Manuel"){
		run("Threshold...");
		waitForUser("Seuillage","Reglage du Seuil");
		getThreshold(lower, upper);
		seuils[1]=lower;
		seuils[2]=upper;	
	} else {	
		if(stack==1) setAutoThreshold(seuils[0]+" dark stack");
		if(stack==0) setAutoThreshold(seuils[0]+" dark");	
	}		
}



function image_dans_pdf(dir,nom,x,y,w){
	origid=getImageID();
	if (File.exists(dir+nom)){
	open(dir+nom);
	run("Size...", "width="+w+" constrain average interpolation=Bilinear");
	getDimensions(width, height, channels, slices, frames);
	run("Select All");
	run("Copy");
	close();
	selectImage(origid);
	makeRectangle(x, y, width, height);
	run("Paste");
	run("Select None");
	}
}

function new_pdf_avec_entete(page){
	newImage("rapport"+page, "RGB white", 2127, 3072, 1);
	image_dans_pdf(dir_argoloj,"logo_entete_mri.jpg",14,44,2100);
	setColor(0, 0, 0);
	setFont("SansSerif", 40, "bold");
	drawString("Argolight Métrologie", 689, 104);
	setFont("SansSerif", 36);
	drawString("Microscope :"+NomMicroscope, 350, 170);
	drawString("Objectif :"+NomObjectif, 350, 220);
	waveString="";
	for(i=0;i<wave.length;i++){
		if (wave[i]!="none") waveString=waveString+" "+wave[i];	
	}
	
	drawString("Longueurs d'ondes :"+waveString, 350, 270);
	

	 
	drawString("Page :"+page, 1570, 120);
	drawString("Date :"+date, 1570, 170);
	drawString("Plateau :"+plateau, 1570, 220);
	drawString("Vérificateur :"+NomVerificateur, 1570, 270);
	

}

function efface_images_temp(){
File.delete(dir_argoloj+"homogeneite.jpg");
	File.delete(dir_argoloj+"resolution.jpg");
	File.delete(dir_argoloj+"dispersion.jpg");
	File.delete(dir_argoloj+"linearite32.jpg");
	File.delete(dir_argoloj+"linearite16.jpg");
	File.delete(dir_argoloj+"coalignement.jpg");
	selectWindow("Log");
	run("Close"); 

}

function validation_nombre(titre,nombre,min,max){
	Dialog.create("Validation");
  Dialog.addSlider(titre, min, max, nombre); //"Number of cells:"
  Dialog.show();
  nombre= Dialog.getNumber();
  return nombre;
}

function infos(){

	/*var plateau=RecupPlateau();
	var NomMicroscope="";
	var TypeMicroscope="";
	var NomObjectif="";
	var NaObjectif="";
	var NomVerificateur="";
	//var Fluo="";
	var wave=newArray(0); //tableau des longueurs d ondes
	var wave_value=newArray(0);*/
	
	Dialog.create("Repertoire pour les resultats");
	Dialog.addCheckbox(dir_argoloj, true);	
	Dialog.show();
	if(!Dialog.getCheckbox()) dir_argoloj=getDirectory("Repertoire pour les resultats");

	Dialog.create("Choix du verificateur");
	tab=RecupNom("Verificateur");
	Dialog.addChoice("Nom du verificateur ",tab,NomVerificateur);
	Dialog.show();
	NomVerificateur = Dialog.getChoice();


	Dialog.create("Choix du microscope");
	tab=RecupNom("Microscope");
	Dialog.addChoice("Nom du microscope ",tab,NomMicroscope);
	Dialog.show();
	NomMicroscope = Dialog.getChoice();


	Dialog.create("Choix de l objectif");
	tab=Recup2Nom("Objectif",NomMicroscope);
	Dialog.addChoice("Nom de l objectif ",tab,NomObjectif);
	Dialog.show();
	NomObjectif = Dialog.getChoice();



	Dialog.create("Choix des longueurs d ondes");
	tab=Recup2Nom("Fluo",NomMicroscope);
	tab=Array.concat("none",tab);
	for (i=0;i<tab.length-1;i++){
		defaut="";
		if (wave.length>i) defaut=wave[i];
		Dialog.addChoice("Cannal "+i+1,tab,defaut);
	}
	Dialog.show();
	wave=newArray(0);
	for (i=0;i<tab.length-1;i++){
		wave=Array.concat(wave,Dialog.getChoice);
		}
	
	Recup_info();
}

function RecupPlateau(){
lines=split(File.openAsString(param), "\n");
row=split(lines[0],";");
return row[1];
}

function RecupNom(nom){
	tab=newArray(0);
	lines=split(File.openAsString(param), "\n");
	for (i=0;i<lines.length;i++){
		row=split(lines[i],";");
		if (row[0]==nom) tab=Array.concat(tab,row[1]);
	}
	return tab;
}


function Recup2Nom(nom,microscope){
	tab=newArray(0);
	lines=split(File.openAsString(param), "\n");
	for (i=0;i<lines.length;i++){
		row=split(lines[i],";");
		if (row[1]==microscope){
			for (j=i;j<lines.length;j++){
				row=split(lines[j],";");
				if (row[0]==nom) tab=Array.concat(tab,row[1]);
				if (row[0]=="end") j=lines.length;
			}		
		} 

}
return tab;	
}

function Recup_info(){
	wave_value=newArray(wave.length);
	lines=split(File.openAsString(param), "\n");
	for (i=0;i<lines.length;i++){
		row=split(lines[i],";");
		if (row[1]==NomMicroscope) {
			TypeMicroscope=row[2];
			for (j=i;j<lines.length;j++){
				row=split(lines[j],";");
				if (row[1]==NomObjectif) NaObjectif=row[2];
				for (k=0;k<wave.length;k++){
					if (wave[k]==row[1]) wave_value[k]=row[2];			
				}
				if (row[0]=="end") j=lines.length;
			
			}		
			
		}	
	}
	Infos=true;
}


function raz_variables(){
	 dir_argoloj=getDirectory("macros")+"toolsets\\ArgoloJ\\";
	 param=dir_argoloj+"parametres_microscopes.csv";
	 plateau=RecupPlateau();
	 NomMicroscope="";
	 TypeMicroscope="";
	 NomObjectif="";
	 NaObjectif="";
	 NomVerificateur="";
	// Fluo="";
	 wave=newArray(0); //tableau des longueurs d ondes
	 wave_value=newArray(0);
	 Infos=false;
	 zero="Oui";
	 date="";
	 datef="";
	//Valeurs d etalonnage donnees par argolight avec la lame
	 Argo_intensites_16 = newArray(1,1.49,2.18,2.99,3.71,4.46,5.18,5.79,6.27,6.88,7.4,7.85,8.18,8.65,9.1,9.55);
	 Argo_intensites_32 = newArray(1,1.52,2.23,3.07,3.91,4.68,5.41,6.05,6.64,7.25,7.78,8.25,8.71,9.18,9.63,10.02);

	 title1 = "Resultats Argolight";
	 title2 = "["+title1+"]";
	 fe = title2;

	// parametres par defaut
	 seuil_calibration=newArray("Li",100,255);
	 seuil_homogeneite=newArray("Otsu",100,255);
	 seuil_coalignement_tot=newArray("Default",100,255);
	 seuil_coalignement=newArray("Yen",100,255);
	 seuil_dispersion=newArray("Minimum",100,255);
	 seuil_resolution=newArray("Default",100,255);
	 seuil_geometrie=newArray("Default",100,255);
	 radius_calibration=1;
	 radius_homogeneite=1;
	 radius_coalignement=2;
	 radius_dispersion=3;
	 radius_resolution=20;
	 radius_linearite=2;
	 taille_calibration=newArray(50,500);
	 taille_homogeneite=newArray(20,100);
	 taille_coalignement=newArray(0.5,10,1);
	 taille_resolution=newArray(2,10000);
	 taille_linearite=newArray(200,1000000);
	 
	 croix=false;

	//Resultats
	// distance_calibration=0;
	// std_calibration=0;
	 std_homo=0;
	 Tab_entete=newArray(0);	
	 Tab_result_moy=newArray(0);
	 distance_z_dispersion=0;
	 bande_resolution=0;
	 decalage_geometrie=0;
	 unit=0;
}


